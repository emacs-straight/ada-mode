;;; WisiToken grammar for Ada
;;
;; Author: Stephen Leake <stephe-leake@stephe-leake.org>
;; Maintainer: Stephen Leake <stephe-leake@stephe-leake.org>
;; Created: Sept 2012
;; Keywords: syntax
;;
;;  Copied from Ada Language Reference Manual Annex P
;;  (https://www.ada-auth.org/standards/ada2x.html), minimal changes
;;  made to conform with WisiToken requirements.

;;  Changes from Annex P:
;;
;;  for WisiToken:
;;
;;  - add single quotes around literal tokens, operators and reserved words, digits.
;;
;;  - delete italics part of token names
;;
;;  - eliminate many redundancies which cause parse conflicts
;;
;;  - replace low level lexical productions with regular expression tokens.
;;
;;  - use nonterminal "tick" for '''.

;; The programs 'annex_p_to_wy.adb' translates the text of ARM Annex P
;; to a WisiToken .wy file, and 'compare_annex_p.adb' compares that
;; with this file. Nonterminals that are changed in this file from ARM
;; Annex P have comments explaining how. Some comments are recognized
;; by 'compare_annex_p.adb' to help with the comparison; see the code
;; for more detail.
;;
;; See ada_license.text for ARM Annex P copyright/license.
;;
;; re2c lexer syntax: https://re2c.org/

%meta_syntax EBNF

%code copyright_license %{
--  Copyright (C) 2013 - 2022 Free Software Foundation, Inc.

--  This program is free software; you can redistribute it and/or
--  modify it under the terms of the GNU General Public License as
--  published by the Free Software Foundation; either version 3, or (at
--  your option) any later version.
--
--  This software is distributed in the hope that it will be useful,
--  but WITHOUT ANY WARRANTY; without even the implied warranty of
--  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
--  General Public License for more details.
--
--  You should have received a copy of the GNU General Public License
--  along with GNU Emacs.  If not, see <https://www.gnu.org/licenses/>.
}%

%generate LALR Ada_Emacs re2c Process
%generate LR1 Ada_Emacs text_rep re2c Process

%language_runtime "Wisi.Ada"

;; wisitoken-parse-lr-mckenzie_recover.adb requires 16.
%max_parallel 20

%case_insensitive
%start compilation

%token <left-paren>  LEFT_PAREN  "("
%token <right-paren> RIGHT_PAREN ")"
%token <left-paren>  LEFT_SQUARE_BRACKET  "["
%token <right-paren> RIGHT_SQUARE_BRACKET "]"

%token <punctuation> AMPERSAND "&"
%token <punctuation> AT_SIGN "@"
%token <punctuation> BAR "|"
%token <punctuation> BOX "<>"
%token <punctuation> COLON ":"
%token <punctuation> COLON_EQUAL ":="
%token <punctuation> COMMA ","
%token <punctuation> DOT "."
%token <punctuation> DOT_DOT ".."
%token <punctuation> EQUAL "="
%token <punctuation> EQUAL_GREATER "=>"
%token <punctuation> GREATER ">"
%token <punctuation> GREATER_EQUAL ">="
%token <punctuation> GREATER_GREATER ">>"
%token <punctuation> LESS "<"
%token <punctuation> LESS_EQUAL "<="
%token <punctuation> LESS_LESS "<<"
%token <punctuation> MINUS "-"
%token <punctuation> PLUS "+"
%token <punctuation> SEMICOLON ";"
%token <punctuation> SLASH "/"
%token <punctuation> SLASH_EQUAL "/="
%token <punctuation> STAR "*"
%token <punctuation> STAR_STAR "**"

;; Support GNAT character coding in names and character and string
;; literals (see test/non_ascii.ads)
%lexer_regexp GNAT_Char_Coding %[ ("[\""[0-9A-Fa-f]+"\"]") ]%

;; re2c lookahead regular expression; this is not a character literal.
;; re2c does not allow combining | with /, so we need three tokens for
;; this (sigh). See wisitoken test character_literal.wy for rationale
;; behind this design.
%token <punctuation> TICK_1 "'"

%token <regexp> TICK_2
    %[ "'" / "('"([\x20-\U0010FFFF]|GNAT_Char_Coding)"'" ]%
  "'"
tick : TICK_1 | TICK_2 ;

;; Handle floating, fixed, exponent, special cases 0.0, (1..Length)
;; and 0..16#10FFFF#
%token <regexp> NUMERIC_LITERAL
    %[ ([0-9])|([0-9_]+([.][0-9]+)?([eE][0-9_-]+)?)|([0-9]+"#"[0-9a-fA-F._-]+"#") ]%
  "1234567890"

;; Include ASCII control chars, except new-line and EOI, in whitespace, just
;; in case they show up in some file (see wisi test_edit_source.adb)
%non_grammar <non-reporting> WHITESPACE %[ [ \x00\x01\x02\x03\x05\x06\x07\x08\t\x0B\x0C\x0D\x0E\x0F\xFF]+ ]%

%non_grammar <new-line> NEW_LINE

;; Ada comments
%non_grammar <comment-new-line> COMMENT "--"

;; gnatprep preprocessor lines
%non_grammar <comment-new-line> GNAT_PREP_IF "#if"
%non_grammar <comment-new-line> GNAT_PREP_ELSIF "#elsif"
%non_grammar <comment-new-line> GNAT_PREP_ELSE "#else"
%non_grammar <comment-new-line> GNAT_PREP_END_IF "#end if"

;; skeleton placeholders. Also terminated by newline, EOI to avoid
;; accidentally commenting out multiple lines.
%non_grammar <comment-one-line> PLACEHOLDER "{" "}"

;; git conflict marks. ada_mode-recover_conflict_marks.adb
%non_grammar <comment-new-line> CONFLICT_MARK_A "<<<<<<<"
%non_grammar <comment-new-line> CONFLICT_MARK_B ">>>>>>>"
%non_grammar <comment-new-line> CONFLICT_MARK_END "======="

;; '$' for gnatprep symbols.
%token <symbol> IDENTIFIER
    %[ ([$a-zA-Z\x80-\U0010FFFF]|GNAT_Char_Coding)([0-9a-zA-Z_\x80-\U0010FFFF]|GNAT_Char_Coding)* ]%
  "A_Bogus_Identifier"

;; \x22 is "
%token <string-double-one-line> STRING_LITERAL
    %[ (["]([\x20-\x21\x23-\U0010FFFF]|GNAT_Char_Coding)*["])+ ]% '""'
%escape_delimiter_doubled STRING_LITERAL

%token <string-single-one-line> CHARACTER_LITERAL %[ "'"([\x20-\U0010FFFF]|GNAT_Char_Coding)"'" ]% "' '"

%elisp_face font-lock-constant-face
%elisp_face font-lock-function-name-face
%elisp_face font-lock-type-face
%elisp_face nil

;;            function name        Ada function   arg_count token_index args
%elisp_indent ada-indent-aggregate Ada_Indent_Aggregate   0
%elisp_indent ada-indent-aspect    Ada_Indent_Aspect      0
%elisp_indent ada-indent-renames   Ada_Indent_Renames_0   1 1
%elisp_indent ada-indent-return    Ada_Indent_Return_0    2 1
%elisp_indent ada-indent-record    Ada_Indent_Record_0    3 1 2
%elisp_indent "ada-indent-record*" Ada_Indent_Record_1    3 2

;; Set McKenzie error recovery costs.
;;
;; We assume the user is typing new code more often than deleting or
;; changing old. Minimal_Complete reduces cost for insert, so the
;; default cost is higher for insert than delete. Often insert requires
;; push_back first, to get the parser in the right state, so push_back
;; is cheaper.
;;
;; We need a balance between Minimal_Complete and other solutions; if
;; Minimal_Complete is too cheap, it will complete the entire
;; compilation_Unit before the error point, when doing something else
;; at some point is better (see test/ada_mode-recover_29.adb, _15,
;; _16, _24). On the other hand, doing lots of Minimal_Complete is
;; very common when writing new code (see
;; test/ada_mode-recover_10.adb, _error_during_resume_01).
;; test/ada_mode-recover_16.adb requires default delete cost 3.
;;
;; test/ada_mode-recover_change_name.adb needs 'ignore check fail' cost 2
;;
;; Default cost for insert, delete, push_back/undo_reduce, ignore check fail.
%mckenzie_cost_default 4 3 2 2
%mckenzie_minimal_complete_cost_delta -3

;; END is cheaper, because needing to insert it is a common case.
%mckenzie_cost_insert END 3

;; make inserting an expression or statement to finish a production
;; cheap; see test/ada_mode-recover_41.adb
%mckenzie_cost_insert IDENTIFIER 2

;; Ada 2020 syntax caused a drastic increase in recursion, which
;; increased the number of parse states with no Minimal_Complete
;; action. So we reduce the cost of ';' and '=>' to compensate; see
;; test/ada_mode-recover_partial_26.
%mckenzie_cost_insert SEMICOLON 3
%mckenzie_cost_insert EQUAL_GREATER 3

;; Make RENAMES more expensive, since object declarations are more
;; common than renames; ada_mode-recover_13.adb
%mckenzie_cost_insert RENAMES 7

;; This cost is applied once, independent of token count.
;; test/ada_mode-recover_40.adb requires cost < 3, so insert 'if
;; identifier then' is as cheap as 'delete else'.
%mckenzie_cost_matching_begin 1

;; Prefer doing more work at the current error point over moving
;; forward to a new one; there is normally only one real error point.
;; But not too much; test/ada_mode-recover_partial_02_lr1.adb requires
;; cost 2.
%mckenzie_cost_fast_forward 2

;; Handle converting 'if' to 'case' (and vice-versa), since that is a
;; common operation. See test/ada_mode-recover_29.adb. This also makes
;; inserting 'end if;' free; that's used a lot. But this defeats
;; Forbid_Minimal_Complete; see
;; test/ada_mode-recover_constant_as_statement_1.adb.
%mckenzie_cost_insert CASE 2
%mckenzie_cost_insert IF 2

;; Prefer undo_reduce over push_back; consider the code:
;;
;; loop
;;    if then
;;       statement;
;;    -- missing 'end if;'
;; end loop;
;; -- ^ error here
;;
;; Error recovery will insert 'end if;', after push_back or
;; undo_reduce of the handled_sequence_of_statements including
;; 'statement'. If it uses push_back, then 'end if;' is before
;; 'statement'; if undo_reduce, after, which is the desired result.
%mckenzie_cost_undo_reduce handled_sequence_of_statements 0
%mckenzie_cost_undo_reduce sequence_of_statements 0
%mckenzie_cost_undo_reduce sequence_of_statements 0
%mckenzie_cost_undo_reduce declarative_part 0

;; Missing or extra closing paren and comma are common; missing
;; handled by Minimal_Complete.
%mckenzie_cost_delete COMMA 1
%mckenzie_cost_insert COMMA 3
%mckenzie_cost_delete RIGHT_PAREN 1
%mckenzie_cost_insert RIGHT_PAREN 3

;; test/ada_mode-recover_partial_02_lr1.adb requires 45_000; wisitoken got 30% faster
%mckenzie_enqueue_limit 58_000

;; 4 required for test/ada_mode-recover_match_names.adb.
%mckenzie_check_limit 4

;; After finding 1 solution, only check 100 more before giving up.
;; Reduces solution time on complex errors.
%mckenzie_check_delta_limit 100

%end_names_optional_option End_Names_Optional

;; This is the first step in a 3 branch conflict below.
%conflict REDUCE defining_identifier_list | REDUCE direct_name on token COLON

%conflict REDUCE defining_identifier_list | REDUCE direct_name on token COMMA
%conflict REDUCE discrete_choice | REDUCE selector_name on token BAR
%conflict REDUCE discrete_choice | REDUCE selector_name on token EQUAL_GREATER
%conflict REDUCE name | REDUCE selector_name on token BAR
%conflict REDUCE name | REDUCE selector_name on token EQUAL_GREATER
%conflict REDUCE record_component_association | REDUCE expression_list on token COMMA
%conflict REDUCE record_component_association | REDUCE expression_list on token RIGHT_PAREN
%conflict REDUCE subtype_indication | REDUCE iterator_specification on token EQUAL_GREATER
%conflict REDUCE subtype_indication | REDUCE iterator_specification on token LOOP
%conflict REDUCE subtype_indication | REDUCE iterator_specification on token USE
%conflict REDUCE subtype_indication | REDUCE primary on token BAR
%conflict REDUCE subtype_indication | REDUCE primary on token COMMA
%conflict REDUCE subtype_indication | REDUCE primary on token EQUAL_GREATER
%conflict REDUCE subtype_indication | REDUCE primary on token RIGHT_PAREN
%conflict SHIFT at_clause | REDUCE name on token USE
%conflict SHIFT attribute_definition_clause | REDUCE attribute_reference on token USE
%conflict SHIFT attribute_definition_clause | REDUCE label_opt on token FOR
%conflict SHIFT dispatching_operation_set | REDUCE extended_global_aspect_element on token COMMA
%conflict SHIFT dispatching_operation_specifier | REDUCE subtype_indication | REDUCE primary on token RIGHT_PAREN
%conflict SHIFT expression_function_declaration | REDUCE subprogram_specification on token IS
%conflict SHIFT formal_derived_type_definition | REDUCE formal_derived_type_definition on token WITH
%conflict SHIFT full_type_declaration | REDUCE discriminant_part on token IS
%conflict SHIFT generic_instantiation | REDUCE parameter_profile on token IS
%conflict SHIFT generic_renaming_declaration | REDUCE generic_formal_part on token FUNCTION
%conflict SHIFT generic_renaming_declaration | REDUCE generic_formal_part on token PACKAGE
%conflict SHIFT generic_renaming_declaration | REDUCE generic_formal_part on token PROCEDURE
%conflict SHIFT global_set | REDUCE formal_parameter_set on token COMMA
%conflict SHIFT global_set | REDUCE global_aspect_element on token COMMA
%conflict SHIFT iterator_filter | REDUCE subtype_indication on token WHEN
%conflict SHIFT membership_choice_list | REDUCE relation on token BAR
%conflict SHIFT null_exclusion | REDUCE mode on token NOT
%conflict SHIFT null_procedure_declaration | REDUCE subprogram_specification on token IS
%conflict SHIFT object_renaming_declaration | REDUCE defining_identifier_list on token COLON
%conflict SHIFT object_renaming_declaration | REDUCE defining_identifier_list | REDUCE direct_name on token COLON
%conflict SHIFT package_body_stub | REDUCE direct_name on token IS
%conflict SHIFT parallel_block_statement | REDUCE label_opt on token PARALLEL
%conflict SHIFT raise_expression | REDUCE raise_expression on token WITH
%conflict SHIFT record_extension_part | REDUCE derived_type_definition on token WITH
%conflict SHIFT tick | REDUCE direct_name on token TICK_1
%conflict SHIFT tick | REDUCE direct_name on token TICK_2
%conflict SHIFT value_sequence | REDUCE array_component_association on token RIGHT_SQUARE_BRACKET

;;; grammar rules

;;:new; for ARM 4.5.7(7), ARM 4.5.8(4) allow "( conditional|quantified_expression )" instead of "(( ... ))"
conditional_quantified_expression
  : if_expression
  | case_expression
  | quantified_expression
  ;

pragma_argument_association_list ;;:new; split out from pragma_g for action
  : pragma_argument_association
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | pragma_argument_association_list ',' pragma_argument_association
    %((wisi-indent-action [nil nil (wisi-hanging nil ada-indent-broken)]))%
  ;

pragma_g
  : 'pragma' IDENTIFIER ['(' pragma_argument_association_list ')'] ';'
    %((wisi-statement-action [1 statement-start 6 statement-end])
      (wisi-face-apply-action [2 nil font-lock-function-name-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken (wisi-anchored 3 1) (wisi-anchored 3 0)
                               ada-indent-broken]))%
  ;; ARM 4.5.7(7), ARM 4.5.8(4) allow "( conditional|quantified_expression )" instead of "(( ... ))"
  | 'pragma' IDENTIFIER ['(' conditional_quantified_expression ')'] ';'
    %((wisi-statement-action [1 statement-start 6 statement-end])
      (wisi-face-apply-action [2 nil font-lock-function-name-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken (wisi-anchored 3 1) (wisi-anchored 3 0)
                               ada-indent-broken]))%
  ;

pragma_argument_association
  ;; [ IDENTIFIER '=>' ] name ;;:redundant with expression
  ;; [IDENTIFIER '=>'] expression ;;:redundant with aspect_mark
  ;; aspect_mark '=>' name ;;:redundant with expression
  : [aspect_mark '=>'] expression
  ;; indent applied in pragma_argument_association_list
  ;

basic_declaration
  : type_declaration
  | subtype_declaration
  | object_declaration
  | number_declaration
  | subprogram_declaration
  | abstract_subprogram_declaration
  | null_procedure_declaration
  | expression_function_declaration
  | package_declaration
  | renaming_declaration
  | exception_declaration
  | generic_declaration
  | generic_instantiation
  ;

;; defining_identifier ;;:redundant with IDENTIFIER
;; ::= IDENTIFIER

type_declaration
  : full_type_declaration
  | incomplete_type_declaration
  | private_type_declaration
  | private_extension_declaration
  ;

full_type_declaration
  : 'type' IDENTIFIER [known_discriminant_part] 'is' type_definition [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 nil font-lock-type-face])

      ;; Indentation of comment after 'is' is
      ;; ada-indent-record-rel-type if type_definition is a record.
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               [ada-indent-broken (ada-indent-record* 'TYPE 5 ada-indent-broken)]
                               (wisi-hanging% ada-indent-broken (* 2 ada-indent-broken))
                               nil ada-indent-broken]))%
  | task_type_declaration
  | protected_type_declaration
  ;

type_definition
  : enumeration_type_definition
  | integer_type_definition
  | real_type_definition
  | array_type_definition
  | record_type_definition
  | access_type_definition
  | derived_type_definition
  | interface_type_definition
  ;

subtype_declaration
  : 'subtype' IDENTIFIER 'is' subtype_indication [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 6 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               ;; test/adacore_9717_001.ads Another_Long_Name
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               nil
                               ada-indent-broken]))%
  ;

subtype_indication
  : [null_exclusion] name [constraint] ;;:edited; subtype_mark inlined
  ;

;; subtype_mark  ;;:inlined
;;  ::= name

constraint
  : scalar_constraint
  | index_constraint
  ;; discriminant_constraint deleted; redundant with subprogram_call in name
  ;

scalar_constraint ::= range_constraint | digits_constraint | delta_constraint ;

;; composite_constraint ;;:deleted; no longer used
;;   : index_constraint
;;   | discriminant_constraint ;;:redundant with expanded actual_parameter_part
;;   ;

assign_value ;;:new; split out from object_declaration assignment_statement, and others for indent
  : ':=' expression
  ;

object_declaration
  : defining_identifier_list ':' ['aliased'] ['constant'] subtype_indication [assign_value]
    [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 8 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               (wisi-hanging% (wisi-anchored% 5 ada-indent-broken)
                                              (wisi-anchored% 5 (* 2 ada-indent-broken)))
                               ada-indent-broken ada-indent-broken]))%
  | defining_identifier_list ':' ['aliased'] ['constant'] access_definition [assign_value]
    [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 8 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               (wisi-hanging% (wisi-anchored% 5 ada-indent-broken)
                                              (wisi-anchored% 5 (* 2 ada-indent-broken)))
                               ada-indent-broken ada-indent-broken]))%
  | defining_identifier_list ':' ['aliased'] ['constant'] array_type_definition [assign_value]
    [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 8 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               (wisi-hanging% (wisi-anchored% 5 ada-indent-broken)
                                              (wisi-anchored% 5 (* 2 ada-indent-broken)))
                               ada-indent-broken ada-indent-broken]))%
  | single_task_declaration
  | single_protected_declaration
  ;

defining_identifier_list
  : IDENTIFIER
    %((wisi-name-action 1))%
  | defining_identifier_list ',' IDENTIFIER
    %((wisi-name-action 3)
      (wisi-indent-action [nil nil ada-indent-broken]))%
  ;

number_declaration
  : defining_identifier_list ':' 'constant' assign_value ';'
    %((wisi-statement-action [1 statement-start 5 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               (wisi-hanging% (wisi-anchored% 4 ada-indent-broken)
                                              (wisi-anchored% 4 (* 2 ada-indent-broken)))
                               ada-indent-broken]))%
  ;

derived_type_definition
  : ['abstract'] ['limited'] 'new' subtype_indication [['and' interface_list] record_extension_part] ;

range_constraint ::= 'range' range_g ;

range_g : range_attribute_reference | simple_expression '..' simple_expression ;

enumeration_literal_list ;;:new for action
  : enumeration_literal_specification {',' enumeration_literal_specification} ;

enumeration_type_definition
  : '(' enumeration_literal_list ')'
    %((wisi-indent-action [nil (wisi-anchored 1 1) (wisi-anchored 1 0)]))%
  ;

enumeration_literal_specification : IDENTIFIER | CHARACTER_LITERAL ;

;; defining_character_literal ::= CHARACTER_LITERAL

integer_type_definition
  : signed_integer_type_definition
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | modular_type_definition
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  ;

signed_integer_type_definition ::= 'range' simple_expression  '..'  simple_expression ;

modular_type_definition ::= 'mod' expression ;

real_type_definition ::= floating_point_definition | fixed_point_definition ;

floating_point_definition ::= 'digits' expression [real_range_specification] ;

real_range_specification : 'range' simple_expression '..' simple_expression ;

fixed_point_definition : ordinary_fixed_point_definition | decimal_fixed_point_definition ;

ordinary_fixed_point_definition : 'delta' expression real_range_specification ;

decimal_fixed_point_definition : 'delta' expression 'digits' expression [real_range_specification] ;

digits_constraint : 'digits' simple_expression [range_constraint] ;

array_type_definition ::= unconstrained_array_definition | constrained_array_definition ;

index_subtype_definition_list : index_subtype_definition {',' index_subtype_definition} ;

unconstrained_array_definition
  : 'array' '(' index_subtype_definition_list ')' 'of' component_definition
    %((wisi-indent-action [nil nil (wisi-anchored 2 1) (wisi-anchored 2 0) nil nil]))%
  ;

index_subtype_definition : name 'range' '<>' ;

discrete_subtype_definition_list : discrete_subtype_definition {',' discrete_subtype_definition} ;

constrained_array_definition
  : 'array' '(' discrete_subtype_definition_list ')' 'of' component_definition
    %((wisi-indent-action [nil nil (wisi-anchored 2 1) (wisi-anchored 2 0) nil nil]))%
  ;

discrete_subtype_definition
  : subtype_indication
  | range_g
  ;

component_definition ::=
    ['aliased'] subtype_indication
  | ['aliased'] access_definition
  ;

index_constraint
  : '(' discrete_range ',' discrete_range {',' discrete_range} ')' ;;:edited single discrete_range redundant with slice
  ;

discrete_range
  : subtype_indication
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | range_g
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  ;

discriminant_part : unknown_discriminant_part | known_discriminant_part ;

unknown_discriminant_part : '(' '<>' ')' ;

discriminant_specification_list ;;:new for action
  : discriminant_specification {';' discriminant_specification} ;

known_discriminant_part
  : '(' discriminant_specification_list ')'
    %((wisi-indent-action [nil (wisi-anchored 1 1) (wisi-anchored 1 0)]))%
  ;

discriminant_specification
  : defining_identifier_list ':' [null_exclusion] name [assign_value]
    %((wisi-face-apply-action [4 font-lock-function-name-face font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging% (wisi-anchored% 4 ada-indent-broken)
                                              (wisi-anchored% 4 (* 2 ada-indent-broken)))]))%
  | defining_identifier_list ':' access_definition [assign_value]
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               (wisi-hanging% (wisi-anchored% 3 ada-indent-broken)
                                              (wisi-anchored% 3 (* 2 ada-indent-broken)))]))%
  ;

;; default_expression ::= expression ;;:inlined

;; discriminant_association_list ;;:redundant with parameter_association_list
;; : discriminant_association {',' discriminant_association} ;

;; discriminant_constraint ;;:redundant with expanded parameter_association_list
;; : '(' parameter_association_list ')'

;; discriminant_association ;;: redundant with expanded parameter_association
;; : component_choice_list assoc_expression

record_type_definition ::= [['abstract'] 'tagged'] ['limited'] record_definition ;

record_definition
  : 'record' component_list 'end' 'record' [IDENTIFIER]
    %((wisi-indent-action [(ada-indent-record* 'TYPE 1 0)
                           [(ada-indent-record* 'TYPE 1 ada-indent) (ada-indent-record* 'TYPE 1 ada-indent)]
                           (ada-indent-record* 'TYPE 1 0)
                           nil ada-indent-broken]))%
  ;; We don't do wisi-match-names here, because the trailing
  ;; identifier is a 2020 addition, and most coding standards don't
  ;; require it.
  | 'null' 'record'
  ;

component_list
  : component_item {component_item}
  | {component_item} variant_part
  |  'null' ';'
    %((wisi-statement-action [1 statement-start 2 statement-end]))%
  ;

component_item ::= component_declaration | aspect_clause ;

component_declaration
  : defining_identifier_list ':' component_definition [assign_value] [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 6 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               (wisi-hanging% (wisi-anchored% 3 ada-indent-broken)
                                              (wisi-anchored% 3 (* 2 ada-indent-broken)))
                               ada-indent-broken ada-indent-broken]))%
  ;

variant_list ;;:new; split out from variant_part for action
  : variant
  | variant_list variant
    %((wisi-motion-action [1 2]))%
  ;

variant_part
  : 'case' direct_name 'is' variant_list 'end' 'case' ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-motion-action [1 4 7])
      (wisi-indent-action [nil ada-indent-broken nil ada-indent-when nil ada-indent-broken ada-indent-broken]))%
  ;

variant
  : 'when' discrete_choice_list '=>' component_list
    %((wisi-statement-action [1 motion])
      ;; ada-indent-when applied in variant_part; consistent with
      ;; discrete_choice_list in case_statement.
      (wisi-indent-action [nil nil ada-indent [ada-indent ada-indent]]))%
  ;

discrete_choice_list
  : discrete_choice
  | discrete_choice_list '|' discrete_choice
  ;

discrete_choice
  : expression ;;:edited choice_expression -> expression
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | subtype_indication
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | range_g
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | 'others'
  ;

record_extension_part
  : 'with' record_definition
    ;; comment after 'with' same as 'record'
    %((wisi-indent-action [[nil (ada-indent-record* 'TYPE 2 0)] nil]))%
  ;

abstract_subprogram_declaration
  : [overriding_indicator] subprogram_specification 'is' 'abstract' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 6 statement-end])
      ;; 'is' consistent with expression_function; same as ada-mode 7.0.1
      (wisi-indent-action [nil nil ada-indent-subprogram-is ada-indent-broken nil  ada-indent-broken]))%
  ;

interface_type_definition
  : ['limited' | 'task' | 'protected' | 'synchronized'] 'interface' ['and' interface_list]
  ;

interface_list
  : name ;;:new; explicit list for action
    %((wisi-face-apply-action [1 font-lock-function-name-face font-lock-type-face]))%
  | interface_list 'and' name ;;:new explicit list for action
    %((wisi-face-apply-action [3 font-lock-function-name-face font-lock-type-face]))%
  ;

access_type_definition
  : [null_exclusion] access_to_object_definition
  | [null_exclusion] 'access' ['protected'] access_to_subprogram_definition ;;:edited
  ;

access_to_object_definition ::= 'access' [general_access_modifier] subtype_indication ;

general_access_modifier ::= 'all' | 'constant' ;

access_to_subprogram_definition
  : 'procedure' parameter_profile ;;:edited; move 'access' ['protected'] to access_type_definition for indent
    ;; test/ada_mode-nominal.ads
    ;; type Procedure_Access_Type_8 is access
    ;;   protected procedure
    ;;     (A_Param : out Integer);
    %((wisi-indent-action [nil (wisi-anchored% 1 ada-indent-broken)]))%
  | 'function' parameter_and_result_profile ;;:edited
    ;; test/ada_mode-nominal.ads
    ;; type Function_Access_Type_10 is access
    ;;   protected function
    ;;     (A_Param : in Float)
    ;;     return Standard.Float;
    %((wisi-indent-action [nil (wisi-anchored% 1 ada-indent-broken)]))%
  ;

null_exclusion ::= 'not' 'null' ;

access_definition
  : [null_exclusion] 'access' ['constant'] name
    %((wisi-face-apply-action [4 font-lock-function-name-face font-lock-type-face]))%

  | [null_exclusion] 'access' ['protected'] 'procedure' parameter_profile
    %((wisi-indent-action [nil nil nil nil (wisi-anchored% 4 ada-indent-broken)]))%

  | [null_exclusion] 'access' ['protected'] 'function' parameter_and_result_profile
    %((wisi-indent-action [nil nil nil nil (wisi-anchored% 4 ada-indent-broken)]))%
  ;

incomplete_type_declaration
  : 'type' IDENTIFIER [discriminant_part] ['is' 'tagged'] ';'
    %((wisi-statement-action [1 statement-start 6 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken]))%
  ;

declarative_item : basic_declarative_item | proper_body | body_stub ;

;; declarative_item is in compilation_unit, as is statement. statement
;; contains pragma_g, so declarative_item can't. But pragma_g must be in the
;; grammar for declarative_part, so we include it here.
declarative_item_pragma
  : declarative_item
  | pragma_g
  ;

declarative_part : [declarative_item_pragma+] ;

basic_declarative_item : basic_declaration | aspect_clause | use_clause ;

;; body  ;;:inlined
;; ::= proper_body | body_stub

proper_body : subprogram_body | package_body | task_body | protected_body ;

name
  : direct_name
    %()%
    %((wisi-propagate-name 1))%

  | explicit_dereference
  ;; indexed_component ;; redundant with 'name actual_parameter_part'
  | slice

  | selected_component
    %()%
    %((wisi-propagate-name 1))%

  | attribute_reference
  ;; type_conversion ;;:redundant with function_call
  | function_call
  | CHARACTER_LITERAL
  | qualified_expression
  ;; name ;; redundant - inlined generalized_reference
  ;; generalized_indexing ;; redundant with function_call
  | target_name
  ;

direct_name
  : IDENTIFIER
    ;; marking this for possible later face-apply, to prevent applying
    ;; face to the aggregate of an allocator, and other similar cases.
    ;; test/ada_mode-nominal.adb
    ;;    Object_3 : access Record_Type_3 := new Record_Type_3 (...)
    %((wisi-face-mark-action [1 suffix]))%
    %((wisi-propagate-name 1))%

  | STRING_LITERAL
    %()%
    %((wisi-propagate-name 1))%
  ;

;; deleted prefix ::= name | implicit_dereference ;; inlined in 'name'

explicit_dereference : name '.' 'all' ;

;; implicit_dereference ;;:inlined; only used in 'prefix'
;; ::= name

;; indexed_component ;;:redundant with function_call
;; ::= name '(' expression {',' expression} ')'

slice
  : name '(' discrete_range ')'
    %((wisi-indent-action [nil nil (wisi-anchored 2 1) (wisi-anchored 2 0)]))%
  ;

selected_component
  : name '.' selector_name
    ;; higher level productions should apply hanging indent to names
    %((wisi-face-mark-action [1 prefix 3 suffix]))%
    %((wisi-merge-names 1 3))%
  ;

selector_name
  : direct_name ;; edited; redundant IDENTIFIER | STRING_LITERAL
  | CHARACTER_LITERAL
  | 'others' ;;:new from component_choice_list, formal_package_actual_part to reduce conflicts
  ;

attribute_reference : name tick attribute_designator | reduction_attribute_reference ;

attribute_designator
  : IDENTIFIER ;; deleted ['(' expression ')'] - redundant with function_call in name
  | 'access'
  | 'delta'
  | 'digits'
  | 'mod'
  ;

range_attribute_reference : name tick range_attribute_designator ;

range_attribute_designator
  : 'range' ['(' expression ')']
    %((wisi-indent-action [nil ada-indent-broken
                               (wisi-hanging (wisi-anchored 2 1)
                                             (wisi-anchored 2 (+ 1 ada-indent-broken)))
                               (wisi-anchored 2 0)]))%
  ;

;; generalized_reference ::= name  ;; inlined to name

;; generalized_indexing ::= name actual_parameter_part - redundant with function_call

aggregate
  : record_aggregate
  | extension_aggregate
  | array_aggregate
  | delta_aggregate
  ;; container_aggregate ;; redundant with array_aggregate

  ;; ARM 4.5.7(7), ARM 4.5.8(4), ARM 4.5.9(4) allow
  ;; ( conditional | quantified_expression | declare_expression )
  ;; instead of (( ... ))
  | '(' conditional_expression ')' ;;:new
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  | '(' quantified_expression ')' ;;:new
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  | '(' declare_expression ')' ;;:new
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  ;

record_aggregate
  : '(' record_component_association_list ')'
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  ;

record_component_association_list
  : record_component_association {',' record_component_association}
  | 'null' 'record'
  ;

record_component_association
  : component_choice_list assoc_expression ;;:edited:; [] inlined for action
    %((wisi-indent-action [nil ada-indent-broken]))%
  | expression ;;new:; [] inlined for action
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  ;; | component_choice_list '=>' '<>' ;;:redundant with expanded assoc_expression
  ;

component_choice_list
  : selector_name
  | component_choice_list '|' selector_name
    ;; This indent is not consistent with discrete_choice_list in
    ;; case_statement, but it is backward-compatible.
    %((wisi-indent-action [nil nil ada-indent-broken]))%
  ;; | 'others' ;;:redundant with expanded selector_name
  ;

extension_aggregate
  : '(' expression 'with' record_component_association_list ')'
    %((wisi-indent-action [nil
                           (wisi-hanging (wisi-anchored 1 1)
                                         (wisi-anchored 1 (+ 1 ada-indent-broken)))
                           (wisi-anchored 1 1)
                           [(wisi-anchored 1 1) (wisi-anchored 1 1)]
                           (wisi-anchored 1 0)]))%
  ;

;; ancestor_part ::= expression | name ;; inlined to 'expression'

array_aggregate ::= positional_array_aggregate | null_array_aggregate | named_array_aggregate ;

expression_list ;; new to support actions
  : expression
    %((wisi-indent-action [[(wisi-hanging nil ada-indent-broken) (wisi-hanging nil ada-indent-broken)]]))%
  | expression_list ',' expression
    %((wisi-indent-action [nil nil [(wisi-hanging nil ada-indent-broken) (wisi-hanging nil ada-indent-broken)]]))%
  ;

positional_array_aggregate
  ;; There must be at least two items in a positional aggregate with
  ;; parens. ARM Annex P enforces this in the syntax rules, but that
  ;; causes too many conflicts, so we relax that here.
  : '(' expression_list ')'
    %((wisi-indent-action [nil
                           [(wisi-anchored 1 1) (wisi-anchored 1 1)]
                           (wisi-anchored 1 0)]))%
  | '(' expression_list ',' 'others' assoc_expression ')'
    %((wisi-indent-action [nil
                           (wisi-anchored 1 1)
                           nil
                           (wisi-anchored 1 1)
                           (wisi-anchored 1 (+ 1 ada-indent-broken))
                           (wisi-anchored 1 0)]))%
  ;; | '(' expression_list ',' 'others' '=>' '<>' ')' ;;:redundant with assoc_expression
  | '[' expression_list [','  'others' assoc_expression] ']'
    %((wisi-indent-action [nil
                           (wisi-anchored 1 1)
                           nil
                           (wisi-anchored 1 1)
                           (wisi-anchored 1 (+ 1 ada-indent-broken))
                           (wisi-anchored 1 0)]))%
  ;; | '[' expression_list ','  'others'  '=>' '<>' ']' ;;:redundant with assoc_expression
  ;

null_array_aggregate ::= '[' ']' ;

named_array_aggregate
  : '(' array_component_association_list ')'
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  | '[' array_component_association_list ']'
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  ;

array_component_association_list ::= array_component_association { ','  array_component_association} ;

array_component_association
  : discrete_choice_list assoc_expression
    ;; This is not consistent with discrete_choice_list in
    ;; case_statement, but it is backward-compatible.
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken) ada-indent-broken]))%
  ;; | discrete_choice_list '=>' '<>' ;;:redundant with expanded assoc_expression
  ;; iterated_component_association ;;:deleted
  | iterated_element_association ;;:new: superset of iterated_component_association
  ;

;; iterated_component_association ;;:deleted; subset of iterated_element_association
;;   : 'for' IDENTIFIER 'in' discrete_choice_list '=>' expression
;;   | 'for' iterator_specification '=>' expression
;;   ;

delta_aggregate ::= record_delta_aggregate | array_delta_aggregate ;

record_delta_aggregate
  : '(' expression 'with' 'delta' record_component_association_list ')'
    %((wisi-indent-action [nil
                           (wisi-hanging (wisi-anchored 1 1)
                                         (wisi-anchored 1 (+ 1 ada-indent-broken)))
                           (wisi-anchored 1 1)
                           (wisi-anchored 1 1)
                           [(wisi-anchored 1 1) (wisi-anchored 1 1)]
                           (wisi-anchored 1 0)]))%
  ;

array_delta_aggregate
  : '(' expression 'with' 'delta' array_component_association_list ')'
    %((wisi-indent-action [nil
                           (wisi-hanging (wisi-anchored 1 1)
                                         (wisi-anchored 1 (+ 1 ada-indent-broken)))
                           (wisi-anchored 1 1)
                           (wisi-anchored 1 1)
                           [(wisi-anchored 1 1) (wisi-anchored 1 1)]
                           (wisi-anchored 1 0)]))%

  | '[' expression 'with' 'delta' array_component_association_list ']'
    %((wisi-indent-action [nil
                           (wisi-hanging (wisi-anchored 1 1)
                                         (wisi-anchored 1 (+ 1 ada-indent-broken)))
                           (wisi-anchored 1 1)
                           (wisi-anchored 1 1)
                           [(wisi-anchored 1 1) (wisi-anchored 1 1)]
                           (wisi-anchored 1 0)]))%
  ;

;; container_aggregate ::= ;; deleted; subset of array_aggregate except for iterated_element_association
;;     null_array_aggregate
;;   | positional_container_aggregate
;;   | named_container_aggregate

;; null_container_aggregate ::= '[' ']' ;; inlined to "null_array_aggregate"

;; positional_container_aggregate ::= '[' expression {','  expression} ']' ;; deleted; only used in container_aggregate

;; named_container_aggregate ::= '[' container_element_association_list ']' ;; deleted; only used in container_aggregate

;; container_element_association_list ::= container_element_association { ','  container_element_association} ;; deleted; only used in container_aggregate

;; container_element_association ::= ;; deleted; only used in container_aggregate
;;     key_choice_list  '=>'  expression
;;   | key_choice_list  '=>'   '<>'
;;   | iterated_element_association

;; key_choice_list ::= key_choice {'|' key_choice} ;; deleted; only used in container_aggregate

;; key_choice ::= expression | discrete_range ;; deleted; only used in container_aggregate

iterated_element_association
  : 'for' loop_parameter_specification ['use' expression] assoc_expression
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               ada-indent-broken]))%
  | 'for' iterator_specification ['use' expression] assoc_expression
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               ada-indent-broken]))%
  ;

AND_relation_list ;;:new, split out from expression for action.
  : 'and' relation
  | AND_relation_list 'and' relation
  ;

AND_THEN_relation_list
  : 'and' 'then' relation
  | AND_THEN_relation_list 'and' 'then' relation
  ;

OR_relation_list
  : 'or' relation
  | OR_relation_list 'or' relation
  ;

OR_ELSE_relation_list
  : 'or' 'else' relation
  | OR_ELSE_relation_list 'or' 'else' relation
  ;

XOR_relation_list
  : 'xor' relation
  | XOR_relation_list 'xor' relation
  ;

expression
  ;; containing productions should apply hanging indent
  : relation ;;:new to reduce conflicts
  | relation AND_relation_list
  | relation AND_THEN_relation_list
  | relation OR_relation_list
  | relation OR_ELSE_relation_list
  | relation XOR_relation_list
  ;

;; choice_expression ;;:deleted; mostly redundant with expression, delete to reduce conflicts
  ;; : choice_relation ;; new to reduce conflicts
  ;; | choice_relation {'and' choice_relation}-        ;; extra -
  ;; | choice_relation {'or' choice_relation}-         ;; extra -
  ;; | choice_relation {'xor' choice_relation}-        ;; extra -
  ;; | choice_relation {'and' 'then' choice_relation}- ;; extra -
  ;; | choice_relation {'or' 'else' choice_relation}-  ;; extra -

relation
  : simple_expression [relational_operator simple_expression]
  | simple_expression ['not'] 'in' membership_choice_list
  | raise_expression
  ;

membership_choice_list ::= membership_choice {'|' membership_choice} ;

membership_choice
  : simple_expression
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | range_g
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  ;; name ;; redundant - from inlined subtype_mark, redundant with name in simple_expression
  ;

simple_expression : [unary_adding_operator] term {binary_adding_operator term} ;

term : factor {multiplying_operator factor} ;

factor ::= primary ['**' primary] | 'abs' primary | 'not' primary ;

primary
  : NUMERIC_LITERAL
    %((wisi-face-apply-action [1 nil font-lock-constant-face]))%
  | 'null'
  ;; STRING_LITERAL ;;:redundant with name
  | aggregate
    %((wisi-indent-action [(ada-indent-aggregate)]))%
  | name
  | allocator
  ;;| '(' expression ')' ;;:redundant with positional_aggregate
  ;; '(' conditional_expression ')' ;;:redundant with aggregate
  ;; '(' quantified_expression ')' ;;:redundant with aggregate
  ;; | '(' declare_expression ')';;:redundant with aggregate
  ;

;; logical_operator ::= 'and' | 'or' | 'xor' ;;:deleted; not used in the grammar

relational_operator : '=' | '/=' | '<' | '<=' | '>' | '>=' ;

binary_adding_operator : '+' | '-' | '&' ;

unary_adding_operator : '+' | '-' ;

multiplying_operator : '*' | '/' | 'mod' | 'rem' ;

;; highest_precedence_operator ::= '**' | 'abs' | 'not' ;; not used in the grammar

conditional_expression : if_expression | case_expression ;

elsif_expression_item ;;:new for action
  : 'elsif' condition 'then' expression
    %((wisi-statement-action [1 motion 3 motion])
      (wisi-motion-action [1 3])
      (wisi-indent-action [nil [ada-indent-broken ada-indent-broken] nil
                               (wisi-hanging* ada-indent ada-indent-broken)]))%
  ;

elsif_expression_list ;;:new for action
  : elsif_expression_item
  | elsif_expression_list elsif_expression_item
    %((wisi-motion-action [1 2]))%
  ;

if_expression
  : 'if' condition 'then' expression [elsif_expression_list] ['else' expression]
    %((wisi-statement-action [1 motion 3 motion 6 motion])
      (wisi-motion-action [1 3 5 6])
      (wisi-indent-action [nil
                           ;; test/ada_mode-conditional_expressions.adb
                           ;; K2b : Integer :=
                           ;;   (if J >
                           ;;      42
                           ;;      --  comment; matches last line of preceding expression
                           ;;    then -1
                           [(wisi-hanging* ada-indent-broken ada-indent-broken)
                            ada-indent-broken]
                           nil
                           ;; test/ada_mode-conditional_expressions.adb A := (if B ...
                           (wisi-hanging* ada-indent ada-indent-broken)
                           nil nil
                           (wisi-hanging* ada-indent ada-indent-broken)]))%
  ;

condition : expression ;

case_expression_alternative_list ;;:new for action
  : case_expression_alternative
  | case_expression_alternative_list ',' case_expression_alternative
    %((wisi-motion-action [1 3]))%
  ;

case_expression
  : 'case' expression 'is' case_expression_alternative_list
    %((wisi-statement-action [1 motion])
      (wisi-motion-action [1 4])
      (wisi-indent-action [nil [(wisi-hanging* ada-indent-broken ada-indent-broken)
                                ada-indent-broken]
                               nil ada-indent-when]))%
  ;

case_expression_alternative
  : 'when' discrete_choice_list '=>' expression
    ;; test/ada_mode-conditional_expressions.adb
    ;; when A =>
    ;;    J = 4
    ;;      or else M,
    ;;
    ;; L4 : Integer := (case J is
    ;;                     when
    ;;                       42
    ;;                       =>
    ;;                        -1 +
    ;;                          2,
    ;;
    ;; The expression is indented ada-indent from 'when' independent
    ;; of any intervening lines (consistent with case_statement_alternative).
    %((wisi-statement-action [1 motion])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               ;; test/ada_mode-conditional_expressions.adb L3 for comment after '=>'
                               (wisi-hanging* ada-indent ada-indent-broken)]))%
  ;

quantified_expression
  : 'for' quantifier loop_parameter_specification assoc_expression
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken]))%
  | 'for' quantifier iterator_specification assoc_expression
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken]))%
  ;

quantifier : 'all' | 'some' ;

;; predicate : expression ; inlined

declare_expression
  : 'declare' {declare_item} 'begin' expression
    %((wisi-indent-action [nil (wisi-block ada-indent) nil (wisi-hanging% ada-indent (+ ada-indent ada-indent-broken))]))%
  ;

declare_item ::= object_declaration | object_renaming_declaration ;

reduction_attribute_reference
  : value_sequence tick reduction_attribute_designator
  ;; name tick reduction_attribute_designator ;; redundant with attribute_reference in name
  ;

value_sequence : '[' ['parallel' ['(' chunk_specification ')']] iterated_element_association ']' ;

reduction_attribute_designator : IDENTIFIER '(' reduction_specification ')' ;

reduction_specification
  : name ',' expression
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)
                           nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)]))%
  ;

;; type_conversion ;;:redundant with function_call
;;   : name '(' expression ')'
;;   | name '(' name ')'
;;   ;

qualified_expression
  : name tick aggregate
    %((wisi-face-apply-action [1 font-lock-function-name-face font-lock-type-face])
      ;; test/indent.ads C_S_Controls, test/ada_mode-long_paren.adb Packet := new Packet_Type'
      (wisi-indent-action [nil (wisi-anchored% 1 ada-indent-broken) (wisi-anchored% 1 ada-indent-broken)]))%
  ;

;; LRM 4.8 has 'subtype_indication' here, which allows a constraint.
;; That allows this derivation:
;;
;; term : factor * factor
;; term : allocator * 4
;; term : new subtype_indication * 4
;; term : new   name range 1 .. a + b   * 4
;;
;; which gives conflicts on *, /, leading to geometric explosion of
;; parallel parsing in arithmetic expressions. LRM 4.8(4) says the
;; constraint is only allowed if the subtype is unconstrained, and the
;; allocator is uninitialized. A constraint for an unconstrained
;; subtype is either an index_constraint or discriminant_constraint;
;; we can eliminate the conflicts by stating that explicitly here.
allocator
  : 'new' [subpool_specification] subtype_indication_paren_constraint
  ;; 'new' [ subpool_specification ] qualified_expression ;;:redundant with name in subtype_indication
  ;
subtype_indication_paren_constraint
  : [null_exclusion] name ;; includes discriminant_constraint; redundant with subprogram_call
  | [null_exclusion] name index_constraint
    %((wisi-face-apply-action [2 font-lock-function-name-face font-lock-type-face]))%
  ;

subpool_specification ::= '(' name ')' ;

sequence_of_statements : statement {statement} {label} ;

sequence_of_statements_opt  ;; new; eliminates many conflicts in entry_call and select
  : [sequence_of_statements]
  ;

;; inlined "[sequence_of_statements]" to "sequence_of_statements_opt"

statement : {label} simple_statement | {label} compound_statement ;

simple_statement
  : null_statement
  | assignment_statement
  | exit_statement
  | goto_statement
  | procedure_call_statement
  | simple_return_statement
  ;; entry_call_statement ;;:redundant with procedure_call_statement
  | requeue_statement
  | delay_statement
  | abort_statement
  | raise_statement
  ;; code_statement ;;:redundant with procedure_call_statement
  | pragma_g ;;:new for pragma
  ;

compound_statement
  : if_statement
  | case_statement
  | loop_statement
  | block_statement
  | extended_return_statement
  | parallel_block_statement
  | accept_statement
  | select_statement
  ;

null_statement
  : 'null' ';'
    %((wisi-statement-action [1 statement-start 2 statement-end]))%
  ;

label
  : '<<' statement_identifier '>>'
    %((wisi-face-apply-action [2 nil font-lock-constant-face])
      (wisi-indent-action [ada-indent-label ada-indent-broken ada-indent-broken]))%
  ;

statement_identifier ::= direct_name ;

assignment_statement
  : name assign_value ';'
    %((wisi-statement-action [1 statement-start 3 statement-end])
      (wisi-indent-action [(wisi-hanging nil ada-indent-broken)
                           (wisi-hanging% (wisi-anchored% 1 ada-indent-broken)
                                          (wisi-anchored% 1 (* 2 ada-indent-broken)))
                           ada-indent-broken]))%
  ;

target_name ::= '@' ;

elsif_statement_item ;;:new for action
  : 'elsif' condition 'then' sequence_of_statements
    %((wisi-statement-action [1 motion 3 motion])
      (wisi-motion-action [1 3])
      (wisi-indent-action [nil [ada-indent-broken ada-indent-broken] nil [(wisi-block ada-indent) ada-indent]]))%
  ;

elsif_statement_list ;;:new for action
  : elsif_statement_item
  | elsif_statement_list elsif_statement_item
  ;

if_statement
  : 'if' condition 'then' sequence_of_statements [elsif_statement_list] ['else' sequence_of_statements] 'end' 'if' ';'
    %((wisi-statement-action [1 statement-start 3 motion 6 motion 10 statement-end])
      (wisi-motion-action [1 3 [5 ELSIF] 6 10])
      (wisi-indent-action [nil
                           [(wisi-hanging* ada-indent-broken ada-indent-broken) ada-indent-broken]
                           nil
                           [(wisi-block ada-indent) ada-indent] nil nil
                           [(wisi-block ada-indent) ada-indent] nil ada-indent-broken ada-indent-broken]))%
  ;

case_statement_alternative_list ;;:new for action
  : case_statement_alternative
  | case_statement_alternative_list case_statement_alternative
  ;

case_statement
  : 'case' expression 'is' case_statement_alternative_list 'end' 'case' ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-motion-action [1 [4 WHEN] 7])
      (wisi-indent-action [nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil [ada-indent-when ada-indent-when]
                           nil ada-indent-broken ada-indent-broken]))%
  ;

case_statement_alternative
  : 'when' discrete_choice_list '=>' sequence_of_statements
    %((wisi-statement-action [1 motion])
      ;; ada-indent-when added in case_statement. The statements are
      ;; indented ada-indent from 'when' independent of any intervening
      ;; lines (consistent with case_expression_alternative).
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               [(wisi-block ada-indent) ada-indent]]))%
  ;

loop_statement
  : label_opt [iteration_scheme] 'loop' sequence_of_statements 'end' 'loop' identifier_opt ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 3 motion 8 statement-end])
      (wisi-motion-action [2 3 8])
      (wisi-indent-action [ada-indent-label nil nil [(wisi-block ada-indent) ada-indent] nil ada-indent-broken
                                            ada-indent-broken ada-indent-broken]))%
    %((wisi-match-names 1 7))%
  ;

iteration_scheme
  : 'while' condition
    %((wisi-indent-action [nil [ada-indent-broken ada-indent-broken]]))%
  | 'for' loop_parameter_specification
    %((wisi-indent-action [nil [ada-indent-broken ada-indent-broken]]))%
  | 'for' iterator_specification
    %((wisi-indent-action [nil [ada-indent-broken ada-indent-broken]]))%
  | ['parallel'] 'for' procedural_iterator
    %((wisi-indent-action [nil nil [ada-indent-broken ada-indent-broken]]))%
  | 'parallel' [ '(' chunk_specification ')' ] 'for' loop_parameter_specification
    %((wisi-indent-action [nil nil (wisi-anchored 1 1) (wisi-anchored 1 0) nil [ada-indent-broken ada-indent-broken]]))%
  | 'parallel' [ '(' chunk_specification ')' ] 'for' iterator_specification
    %((wisi-indent-action [nil nil (wisi-anchored 1 1) (wisi-anchored 1 0) nil [ada-indent-broken ada-indent-broken]]))%
  ;

chunk_specification
  : simple_expression
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | IDENTIFIER 'in' discrete_subtype_definition
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken]))%
  ;

loop_parameter_specification
  ;;  This is very similar to iterator_specification;
  ;;  discrete_subtype_definition is a superset of 'name'. Could merge
  ;;  to reduce conflicts.
  : IDENTIFIER 'in' ['reverse'] discrete_subtype_definition [iterator_filter]
  ;

iterator_filter ::= 'when' condition ;

iterator_specification
  : IDENTIFIER [':' loop_parameter_subtype_indication] 'in' ['reverse'] name [iterator_filter]
  ;; "name" here names an iterator function, not a type

  | IDENTIFIER [':' loop_parameter_subtype_indication] 'of' ['reverse'] name [iterator_filter]
  ;; "name" here names an object, not a type
  ;

loop_parameter_subtype_indication ::= subtype_indication | access_definition ;

procedural_iterator
  : iterator_parameter_specification 'of' function_call [iterator_filter] ;;:edited iterator_procedure_call => function_call
  ;

iterator_parameter_specification ::=
    formal_part
  | '(' defining_identifier_list ')' ;; 'IDENTIFIER {',' IDENTIFIER}' redundant with defining_identifier_list
  ;

;; iterator_procedure_call ;;:redundant with expanded function_call
;;   ::=  name
;;   | name iterator_actual_parameter_part

;; iterator_actual_parameter_part ;;:redundant with expanded actual_parameter_part
;; ::= '(' iterator_parameter_association { ',' iterator_parameter_association} ')'

;; iterator_parameter_association ;;:redundant with expanded parameter_association
;; : parameter_association | parameter_association_with_box ;

;; parameter_association_with_box ;;:redundant with expanded parameter_association
;; : [ selector_name '=>' ] '<>' ;

identifier_opt ;;:new; split out from block_statement and other named statements for wisi-match-names
  : [IDENTIFIER] ;

label_opt ;;:new; split out from block_statement for match_names
  : [statement_identifier ':']
    %()%
    %((wisi-propagate-name 1))%
  ;

block_statement
  : label_opt ['declare' declarative_part] 'begin' handled_sequence_of_statements
    'end' identifier_opt ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 4 statement-override 8 statement-end])
      (wisi-motion-action [1 2 4 [5 WHEN] 8])
      (wisi-indent-action [ada-indent-label nil [(wisi-block ada-indent) ada-indent] nil
                                            [(wisi-block ada-indent) ada-indent]
                                            nil ada-indent-broken ada-indent-broken]))%
    %((wisi-match-names 1 7))%
  ;

statement_AND_list
  : handled_sequence_of_statements
  | statement_AND_list 'and' handled_sequence_of_statements
    %((wisi-indent-action [nil (- ada-indent) nil]))%
  ;

parallel_block_statement
  : 'parallel' 'do' handled_sequence_of_statements 'and' statement_AND_list 'end' 'do' ';'
    %((wisi-indent-action [nil nil [(wisi-block ada-indent) ada-indent] nil [(wisi-block ada-indent) ada-indent]
                               nil nil nil]))%
  ;

exit_statement
  : 'exit' [name] ['when' condition] ';'
    %((wisi-statement-action [1 statement-start 5 statement-end])
      (wisi-indent-action [nil nil nil ada-indent-broken ada-indent-broken]))%
  ;

goto_statement
  : 'goto' name ';'
    %((wisi-statement-action [1 statement-start 3 statement-end])
      (wisi-face-apply-action [2 nil font-lock-constant-face]))%
  ;

subprogram_declaration
  : [overriding_indicator] subprogram_specification [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 4 statement-end]))%
  ;

subprogram_specification
  : procedure_specification
    %()%
    %((wisi-propagate-name 1))%
  | function_specification
    %()%
    %((wisi-propagate-name 1))%
  ;

procedure_specification
  : 'procedure' name parameter_profile
    %((wisi-statement-action [1 statement-start])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 font-lock-function-name-face font-lock-function-name-face])
      ;; Comments after 'name' are assumed to be present only when
      ;; 'parameter_profile_opt' is empty, so they are before the 'is'
      ;; in a subprogram_body; indent the same as after a
      ;; subprogram_declaration (see
      ;; ada_mode-options-markus_column_0.adb). Similarly for comments
      ;; after 'parameter_profile_opt'.
      (wisi-indent-action [nil
                           [(wisi-hanging* ada-indent-broken ada-indent-broken)
                            nil]
                           [ada-indent-broken nil]]))%
    %((wisi-propagate-name 2))%
  ;

function_specification
  : 'function' name parameter_and_result_profile
    %((wisi-statement-action [1 statement-start])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken]))%
    %((wisi-propagate-name 2))%
  ;

;; designator ::= [name  '.'] IDENTIFIER | STRING_LITERAL ;;:inlined to "name"

;; defining_designator ::= name | STRING_LITERAL ;;:inlined to "name"

;; defining_program_unit_name ::= [name  '.'] IDENTIFIER ;;:inlined to "name"; redundant with selected_component in name

;; operator_symbol ::= STRING_LITERAL ;;:inlined

;; defining_operator_symbol ::= STRING_LITERAL ;;:inlined

parameter_profile ::= [formal_part] ;

result_profile ;;:new; split out from parameter_and_result_profile for indent actions
  : 'return' [null_exclusion] name
    %((wisi-face-apply-action [3 font-lock-function-name-face font-lock-type-face])
      (wisi-indent-action [nil
                           (wisi-anchored% 1 ada-indent-broken)
                           (wisi-hanging
                            (wisi-anchored% 1 ada-indent-broken)
                            (wisi-anchored% 1 ada-indent-broken))]))%
  | 'return' access_definition
    ;; test/ada_mode-nominal.adb
    ;; function Function_Access_1
    ;;   (A_Param : in Float)
    ;;   return access
    ;;     Standard.Float
    %((wisi-indent-action [nil (wisi-anchored% 1 ada-indent-broken)]))%
  ;

parameter_and_result_profile
  : formal_part result_profile ;;:inlined for action
    %((wisi-indent-action [nil (ada-indent-return 1 0)]))%
  | result_profile ;;:new
  ;

parameter_specification_list ;;:new; split out from formal_part for action
  : parameter_specification {';' parameter_specification}
  ;

formal_part
  : '(' parameter_specification_list ')'
    %((wisi-statement-action [1 misc]) ;; for ada-wisi-in-paramlist-p, ada-goto-declarative-region-start
      (wisi-indent-action [nil (wisi-anchored 1 1) (wisi-anchored 1 0)]))%
  ;

parameter_specification
  : defining_identifier_list ':' ['aliased'] mode [null_exclusion] name [assign_value]
    %((wisi-face-apply-action [6 font-lock-function-name-face font-lock-type-face])
      (wisi-indent-action [nil
                           ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                           ;; test/ada_mode-parens.ads Function_1
                           (wisi-hanging (wisi-anchored% 2 ada-indent-broken)
                                         (wisi-anchored% 2 ada-indent-broken))
                           (wisi-hanging% (wisi-anchored% 2 ada-indent-broken)
                                          (wisi-anchored% 2 (* 2 ada-indent-broken)))]))%
  | defining_identifier_list ':' access_definition [assign_value]
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging% (wisi-anchored% 3 ada-indent-broken)
                                              (wisi-anchored% 3 (* 2 ada-indent-broken)))]))%
  ;

mode : ['in'] | 'in' 'out' | 'out' ;

global_aspect_definition
  ;;  'null' ;;deleted; redundant with expression in aspect
  ;; | 'unspecified'  ;;deleted: not a keyword
  : global_mode global_designator
  |  '(' global_aspect_element{ ','  global_aspect_element} ')'
  | extended_global_aspect_definition
  ;

global_aspect_element ::= global_mode global_set | extended_global_aspect_element ;

global_mode ::= basic_global_mode | extended_global_mode ;

basic_global_mode ::= 'in' | 'in' 'out' | 'out' ;

global_set ::= name { ','  name} ;

global_designator ::= 'all' | 'synchronized' | name ;

;; global_name ::= name | name ;;:inlined to "name"

name_opt ;;:new; split out from subprogram_body for wisi-match-names
  : [name]
    %()%
    %((wisi-propagate-name 1))%
  ;

subprogram_body
  : [overriding_indicator] subprogram_specification [aspect_specification] 'is' declarative_part
    'begin' handled_sequence_of_statements 'end' name_opt ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 4 motion 6 motion 10 statement-end])
      (wisi-motion-action [1 2 4 6 [7 WHEN] 10])
      (wisi-face-apply-action [9 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil nil nil nil [(wisi-block ada-indent) ada-indent]
                               nil [(wisi-block ada-indent) ada-indent] nil ada-indent-broken ada-indent-broken]))%
    %((wisi-match-names 2 9))%
  ;

procedure_call_statement
  : name ';'
    %((wisi-statement-action [1 statement-start 2 statement-end])
      (wisi-indent-action [(wisi-hanging nil ada-indent-broken) ada-indent-broken]))%
  ;; name actual_parameter_part ';' ;;:redundant with function_call in name
  ;

function_call
  ;; name ;;:redundant with "name"
  : name actual_parameter_part
    ;; Using anchored is the only way to ensure enclosing indents are ignored.
    %((wisi-indent-action [nil (wisi-anchored% 1 ada-indent-broken)]))%
  ;

parameter_association_list ;; new to support actions
  : parameter_association
  | parameter_association_list ',' parameter_association
  ;

actual_parameter_part
  ;; '(' parameter_association {',' parameter_association} ')' ;;:deleted to support actions
  : '(' parameter_association_list ')' ;; new to support actions
    ;; see note in test/ada_mode-parens.adb Local_10 about right paren indent.
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%

  ;; ARM 4.5.7(7), ARM 4.5.8(4), ARM 4.5.9(4) allow
  ;; ( conditional | quantified_expression | declare_expression)
  ;; instead of (( ... ))
  | '(' conditional_expression ')' ;; new
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  | '(' quantified_expression ')' ;; new
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  | '(' declare_expression ')' ;;:new
    %((wisi-indent-action [nil [(wisi-anchored 1 1) (wisi-anchored 1 1)] (wisi-anchored 1 0)]))%
  ;

assoc_expression ;;:new; split out from *_association for indent
  : '=>' expression
    ;; Using anchor is the only way to ignore subsequent indents
    %((wisi-indent-action [nil (wisi-hanging%
                                (wisi-anchored% 1 ada-indent-broken)
                                (wisi-anchored% 1 (* 2 ada-indent-broken)))]))%
  | '=>' '<>' ;;:new from iterator_parameter_association to reduce conflicts
  ;

parameter_association
  : component_choice_list assoc_expression ;;:inlined for action, expanded to component_choice_list to reduce conflicts
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken]))%
  | expression ;;:inlined for action
    %((wisi-indent-action [(wisi-hanging nil ada-indent-broken)]))%
  | '<>' ;;:new from iterator_parameter_association to reduce conflicts
  ;

;; explicit_actual_parameter ;;:inlined
;; : expression
;; ;; name ;;:redundant with expression
;; ;

simple_return_statement
  : 'return' [expression]';'
    ;; test/ada_mode-parens.adb Function_3
    ;; return
    ;;   1.0 +
    ;;   2.0;
    %((wisi-statement-action [1 statement-start 3 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken]))%
  ;

extended_return_object_declaration
  : IDENTIFIER ':' ['aliased'] ['constant'] return_subtype_indication [assign_value]
    %((wisi-indent-action [nil nil ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging% (wisi-anchored% 5 ada-indent-broken)
                                              (wisi-anchored% 5 (* 2 ada-indent-broken)))]))%
  ;

extended_return_statement
  : 'return' extended_return_object_declaration ['do' handled_sequence_of_statements 'end' 'return'] ';'
    ;; indent the same as a simple return statement
    ;; test/ada_mode-nominal.adb
    ;; return B : Integer :=
    ;;   (Local_Function);
    ;; (wisi-indent-action [nil nil nil]))
    %((wisi-statement-action [1 statement-start 3 motion 7 statement-end])
      (wisi-motion-action [1 3 [4 WHEN] 7])
      (wisi-indent-action [[nil ada-indent] ada-indent-broken nil [(wisi-block ada-indent) ada-indent] nil
                           ada-indent-broken ada-indent-broken]))%
  ;

return_subtype_indication : subtype_indication | access_definition ;

null_procedure_declaration
  : [overriding_indicator] procedure_specification 'is' 'null' [aspect_specification] ';'
    ;; indent IS, WITH consistent with expression_function
    %((wisi-statement-action [1 statement-start 2 statement-override 6 statement-end])
      (wisi-indent-action [nil nil ada-indent-subprogram-is ada-indent-broken nil ada-indent-broken]))%
  ;

expression_function_declaration
  ;;:redundant with 'aggregate'
  ;; [overriding_indicator] function_specification 'is' '(' expression ')' [aspect_specification] ';'

  : [overriding_indicator] function_specification 'is' aggregate [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 6 statement-end])
      (wisi-indent-action [nil nil ada-indent-subprogram-is ada-indent-broken nil ada-indent-broken]))%
  ;

package_declaration
  : package_specification ';'
    %((wisi-statement-action [1 statement-start 2 statement-end])
      (wisi-motion-action [1 2]))%
  ;

;; See comment at declarative_item_pragma for pragma_g
basic_declarative_item_pragma
  : basic_declarative_item
  | pragma_g
  ;

basic_declarative_item_list ;;:new for action in package_specification
  : basic_declarative_item_pragma+
  ;

;; replace "[[name '.' ] IDENTIFIER" with "name_opt" for consistency and for actions
package_specification
  : 'package' name [aspect_specification] 'is' [basic_declarative_item_list]
    ['private' [basic_declarative_item_list]] 'end' name_opt
    %((wisi-statement-action [1 statement-start 4 motion 6 motion])
      (wisi-name-action 2)
      (wisi-motion-action [1 4 6])
      (wisi-face-apply-action [2 font-lock-function-name-face font-lock-function-name-face
                                 9 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil nil [(wisi-block ada-indent) ada-indent] nil
                           [(wisi-block ada-indent) ada-indent] nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)]))%
    %((wisi-match-names 2 9))%
  ;

;; replace "[[name '.' ] IDENTIFIER" with "name" for consistency and for actions
package_body
  : 'package' 'body' name [aspect_specification] 'is' declarative_part
    ['begin' handled_sequence_of_statements] 'end' name_opt ';'
    %((wisi-statement-action [1 statement-start 5 motion 7 motion 11 statement-end])
      (wisi-name-action 3)
      (wisi-motion-action [1 5 7 [8 WHEN] 13])
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face
                                 10 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil nil [(wisi-block ada-indent) ada-indent]
                           nil [(wisi-block ada-indent) ada-indent] nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken]))%
    %((wisi-match-names 3 10))%
  ;

private_type_declaration
  : 'type' IDENTIFIER [discriminant_part] 'is' [['abstract'] 'tagged'] ['limited'] 'private' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 10 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken nil ada-indent-broken ada-indent-broken
                               ada-indent-broken ada-indent-broken nil ada-indent-broken]))%
  ;

private_extension_declaration
  : 'type' IDENTIFIER [discriminant_part] 'is' ['abstract'] ['limited' | 'synchronized'] 'new'
    subtype_indication ['and' interface_list] 'with' 'private' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 15 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken ada-indent-broken nil ada-indent-broken]))%
  ;

overriding_indicator
  : ['not'] 'overriding'
    %((wisi-statement-action [1 statement-start 2 statement-override]))%
  ;

use_clause : use_package_clause | use_type_clause ;

name_list ;;:new; split out from use_package_clause for action
  : name
  | name_list ',' name
  ;

use_package_clause
  : 'use' name_list ';'
    %((wisi-statement-action [1 statement-start 3 statement-end])
      (wisi-face-apply-list-action [2 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil ada-indent-use ada-indent-broken]))%
  ;

use_type_clause
  ::= 'use' ['all'] 'type' name_list ';'
    %((wisi-statement-action [1 statement-start 5 statement-end])
      (wisi-face-apply-list-action [4 font-lock-function-name-face font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-use ada-indent-broken]))%
  ;

renaming_declaration
  : object_renaming_declaration
  | exception_renaming_declaration
  | package_renaming_declaration
  | subprogram_renaming_declaration
  | generic_renaming_declaration
  ;

object_renaming_declaration
  : IDENTIFIER [':' [null_exclusion] name] 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 8 statement-end])
      (wisi-name-action 1)
      (wisi-face-apply-action [4 font-lock-function-name-face font-lock-type-face])
      (wisi-indent-action [nil
                           ada-indent-broken ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken ada-indent-broken]))%
  | IDENTIFIER ':' access_definition 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 1)
      (wisi-indent-action [nil
                           ada-indent-broken ada-indent-broken ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken ada-indent-broken]))%
  ;

exception_renaming_declaration
  : IDENTIFIER ':' 'exception' 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 1)
      (wisi-face-apply-action [5 font-lock-function-name-face nil])
      (wisi-indent-action [nil
                           ada-indent-broken ada-indent-broken ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken ada-indent-broken]))%
  ;

package_renaming_declaration
  : 'package' name 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 6 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 font-lock-function-name-face font-lock-function-name-face
                                 4 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil ada-indent-broken]))%
  ;

subprogram_renaming_declaration
  : [overriding_indicator] subprogram_specification 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 6 statement-end])
      (wisi-face-apply-action [4 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil nil (ada-indent-renames 2)
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               nil ada-indent-broken]))%
  ;

generic_renaming_declaration
  : 'generic' 'package' name 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face
                                 5 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           ada-indent-broken]))%
  | 'generic' 'procedure' name 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face
                                 5 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           ada-indent-broken]))%
  | 'generic' 'function' name 'renames' name [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face
                                 5 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           ada-indent-broken]))%
  ;

task_type_declaration
  : 'task' 'type' IDENTIFIER [known_discriminant_part] [aspect_specification]
    ['is' ['new' interface_list 'with'] task_definition] ';'
    %((wisi-statement-action [1 statement-start 6 motion 9 motion 11 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 nil font-lock-type-face 12 nil font-lock-type-face])
      (wisi-motion-action [1 6 9 [10 PRIVATE] 11])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken nil
                               [nil ada-indent] ada-indent-broken ada-indent-broken
                               ada-indent-broken nil ada-indent-broken]))%
    %((wisi-match-names 3 10))%
  ;

single_task_declaration
  : 'task' IDENTIFIER [aspect_specification] ['is' ['new' interface_list 'with'] task_definition] ';'
    %((wisi-statement-action [1 statement-start 4 motion 9 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 nil font-lock-type-face])
      (wisi-motion-action [1 4 [8 PRIVATE] 11])
      (wisi-indent-action [nil ada-indent-broken nil [nil ada-indent] ada-indent-broken
                               ada-indent-broken ada-indent-broken nil ada-indent-broken]))%
    %((wisi-match-names 2 8))%
  ;

task_item_list ;;:new; split out from task_definition for action
  : task_item
  | task_item_list task_item
  ;

task_definition
  : task_item_list [ 'private' task_item_list] 'end' identifier_opt
    %((wisi-statement-action [2 motion])
      (wisi-face-apply-action [5 nil font-lock-type-face])
      (wisi-indent-action [[ada-indent ada-indent] nil [ada-indent ada-indent] nil ada-indent-broken]))%
    %((wisi-propagate-name 5))%
  ;

task_item : entry_declaration | aspect_clause ;

task_body
  : 'task' 'body' IDENTIFIER [aspect_specification] 'is' declarative_part
    'begin' handled_sequence_of_statements 'end' identifier_opt ';'
    %((wisi-statement-action [1 statement-start 5 motion 7 motion 11 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 nil font-lock-type-face 10 nil font-lock-type-face])
      (wisi-motion-action [1 5 7 [8 WHEN] 11])
      (wisi-indent-action [nil nil ada-indent-broken nil nil [(wisi-block ada-indent) ada-indent]
                               nil [(wisi-block ada-indent) ada-indent] nil ada-indent-broken ada-indent-broken]))%
    %((wisi-match-names 3 10))%
  ;

protected_type_declaration
  :'protected' 'type' IDENTIFIER [known_discriminant_part] [aspect_specification] 'is'
    ['new' interface_list 'with'] protected_definition ';'
    %((wisi-statement-action [1 statement-start 6 motion 11 statement-end])
      (wisi-name-action 3)
      (wisi-motion-action [1 6 [10 PRIVATE] 11])
      (wisi-face-apply-action [3 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken nil [nil ada-indent]
                               ada-indent-broken ada-indent-broken ada-indent-broken
                               [nil ada-indent-broken] ada-indent-broken]))%
    %((wisi-match-names 3 10))%
  ;

single_protected_declaration
  : 'protected' IDENTIFIER [aspect_specification] 'is' ['new' interface_list 'with'] protected_definition ';'
    %((wisi-statement-action [1 statement-start 4 motion 7 motion 9 statement-end])
      (wisi-motion-action [1 4 7 [8 PRIVATE] 9])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken nil [nil ada-indent] ada-indent-broken
                               ada-indent-broken ada-indent-broken nil ada-indent-broken]))%
    %((wisi-match-names 2 8))%
  ;

protected_operation_declaration_list ;;:new; split out from protected_definition for actions
  : protected_operation_declaration
  | protected_operation_declaration_list protected_operation_declaration
  ;

protected_element_declaration_list ;;:new; split out from protected_definition for actions
  : protected_element_declaration
  | protected_element_declaration_list protected_element_declaration
  ;

protected_definition
  : [protected_operation_declaration_list] [ 'private' [protected_element_declaration_list] ] 'end' [IDENTIFIER]
    %((wisi-statement-action [2 motion])
      (wisi-face-apply-action [5 nil font-lock-type-face])
      (wisi-indent-action [[ada-indent ada-indent] nil [ada-indent ada-indent] nil ada-indent-broken]))%
    %((wisi-propagate-name 5))%
  ;

protected_operation_declaration
  : subprogram_declaration
  | pragma_g ;;:new for pragma
  | entry_declaration
  | aspect_clause
  ;

protected_element_declaration ::= protected_operation_declaration | component_declaration ;

protected_operation_item_list ;;:new; split out from protected_body for actions
  : protected_operation_item
  | protected_operation_item_list protected_operation_item
  ;

protected_body
  : 'protected' 'body' IDENTIFIER [aspect_specification] 'is' [protected_operation_item_list] 'end' identifier_opt ';'
    %((wisi-statement-action [1 statement-start 5 motion 9 statement-end])
      (wisi-name-action 3)
      (wisi-motion-action [1 5 9])
      (wisi-face-apply-action [3 nil font-lock-type-face 8 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken nil nil [(wisi-block ada-indent) ada-indent]
                               nil ada-indent-broken ada-indent-broken]))%
    %((wisi-match-names 3 8))%
  ;

protected_operation_item
  : subprogram_declaration
  | subprogram_body
  | null_procedure_declaration
  | expression_function_declaration
  | entry_body
  | aspect_clause
  ;

;; synchronization_kind ::= By_Entry | By_Protected_Procedure | Optional ;; not used in the grammar

entry_declaration
  : [overriding_indicator] 'entry' IDENTIFIER ['(' discrete_subtype_definition ')'] parameter_profile
    [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 9 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 nil font-lock-function-name-face])
      (wisi-indent-action [nil nil ada-indent-broken ada-indent-broken (wisi-anchored 4 1) (wisi-anchored 4 0)
                               ada-indent-broken nil ada-indent-broken]))%
  ;

accept_statement ::=
    'accept' direct_name ['(' entry_index ')'] parameter_profile
    ['do' handled_sequence_of_statements 'end' identifier_opt] ';'
    %((wisi-statement-action [1 statement-start 7 motion 11 statement-end])
      (wisi-motion-action [1 7 [8 WHEN] 11])
      (wisi-face-apply-action [2 nil font-lock-function-name-face 10 nil font-lock-function-name-face])
      (wisi-indent-action [nil ada-indent-broken
                               ada-indent-broken nil nil ;; we assume the entry index tokens are always on one line
                               ada-indent-broken
                               [nil ada-indent]
                               [(wisi-block ada-indent) ada-indent]
                               nil ada-indent-broken ada-indent-broken]))%
    %((wisi-match-names 2 10))%
  ;

entry_index ::= expression ;

entry_body
  : 'entry' IDENTIFIER entry_body_formal_part [aspect_specification] entry_barrier 'is' declarative_part 'begin'
    handled_sequence_of_statements 'end' identifier_opt ';'
    %((wisi-statement-action [1 statement-start 5 motion 6 motion 8 motion 12 statement-end])
      (wisi-name-action 2)
      (wisi-motion-action [1 5 6 8 [9 WHEN] 12])
      (wisi-face-apply-action [2 nil font-lock-function-name-face 11 nil font-lock-function-name-face])

      ;; test/ada_mode-nominal.adb
      ;; entry E2
      ;;   (X : Integer)
      ;;   when Local_1 = 0 and not
      ;;     (Local_2 = 1)
      ;;
      ;; entry E3
      ;;   (X : Integer) when Local_1 = 0 and not
      ;;     (Local_2 = 1)
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken nil ada-indent-broken
                               nil [ada-indent ada-indent] nil [ada-indent ada-indent] nil ada-indent-broken
                               ada-indent-broken]))%
    %((wisi-match-names 2 11))%
  ;

entry_body_formal_part
  : ['(' entry_index_specification ')'] parameter_profile
    %((wisi-indent-action [nil (wisi-anchored 1 1) (wisi-anchored 1 0) nil]))%
  ;

entry_barrier
  : 'when' condition
    %((wisi-statement-action [1 motion])
      (wisi-indent-action [nil (wisi-hanging* ada-indent-broken ada-indent-broken)]))%
  ;

entry_index_specification ::= 'for' IDENTIFIER 'in' discrete_subtype_definition ;

;; entry_call_statement ::= name [actual_parameter_part] ';' ;; deleted; redundant with procedure_call_statement

requeue_statement
  : 'requeue' name ['with' 'abort'] ';'
    %((wisi-statement-action [1 statement-start 5 statement-end]))%
  ;

delay_statement : delay_until_statement | delay_relative_statement ;

delay_until_statement
  : 'delay' 'until' expression ';'
    %((wisi-statement-action [1 statement-start 4 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken]))%
  ;

delay_relative_statement
  : 'delay' expression ';'
    %((wisi-statement-action [1 statement-start 3 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken]))%
  ;

select_statement
  : selective_accept
  | timed_entry_call
  | conditional_entry_call
  | asynchronous_select
  ;

guard_select ;;:new; split out from selective_accept for actions
  : guard select_alternative
    %((wisi-indent-action [nil (wisi-block ada-indent)]))%
  | select_alternative
  ;

select_alternative_list ;;:new; split out from selective_accept for actions
  : guard_select
  | select_alternative_list 'or' guard_select
    %((wisi-statement-action [2 motion])
      (wisi-motion-action [[1 OR] 2])
      (wisi-indent-action [nil (- ada-indent) nil]))%
  ;

selective_accept
  : 'select' select_alternative_list [ 'else' sequence_of_statements ] 'end' 'select' ';'
    %((wisi-statement-action [1 statement-start 3 motion 7 statement-end])
      (wisi-motion-action [1 [2 OR] 3 7])
      (wisi-indent-action [[nil ada-indent] [(wisi-block ada-indent) ada-indent] nil
                           [(wisi-block ada-indent) ada-indent] nil ada-indent-broken
                           ada-indent-broken]))%
  ;

guard
  : 'when' condition '=>'
    %((wisi-indent-action [nil (wisi-anchored 1 ada-indent-broken) (wisi-anchored 1 ada-indent-broken)]))%
  ;

select_alternative
  : accept_alternative
  | delay_alternative
  | terminate_alternative
  ;

accept_alternative
  : accept_statement sequence_of_statements_opt
  ;

delay_alternative
  : delay_statement sequence_of_statements_opt
  ;

terminate_alternative
  : 'terminate' ';'
    %((wisi-statement-action [1 statement-start 2 statement-end])
      (wisi-indent-action [nil ada-indent-broken]))%
  ;

timed_entry_call
  : 'select' entry_call_alternative 'or' delay_alternative 'end' 'select' ';'
    %((wisi-statement-action [1 statement-start 3 motion 6 statement-end])
      (wisi-motion-action [1 3 7])
      (wisi-indent-action [nil [(wisi-block ada-indent) ada-indent] nil [(wisi-block ada-indent) ada-indent] nil
                               ada-indent-broken ada-indent-broken]))%
  ;

entry_call_alternative : procedure_call_statement sequence_of_statements_opt ;

;; procedure_or_entry_call ::= procedure_call_statement | entry_call_statement ;; inlined to procedure_call_statement

conditional_entry_call
  : 'select' entry_call_alternative 'else' sequence_of_statements 'end' 'select' ';'
    %((wisi-statement-action [1 statement-start 3 motion 7 statement-end])
      (wisi-motion-action [1 3 7])
      (wisi-indent-action [nil [(wisi-block ada-indent) ada-indent] nil [(wisi-block ada-indent) ada-indent] nil
                               ada-indent-broken ada-indent-broken]))%
  ;

asynchronous_select
  : 'select' triggering_alternative 'then' 'abort' abortable_part 'end' 'select' ';'
    %((wisi-statement-action [1 statement-start 3 motion 8 statement-end])
      (wisi-motion-action [1 3 8])
      (wisi-indent-action [nil [(wisi-block ada-indent) ada-indent] [nil ada-indent-broken] ada-indent-broken
                               [(wisi-block ada-indent) ada-indent] nil ada-indent-broken ada-indent-broken]))%
  ;

triggering_alternative
  ;; triggering_statement [sequence_of_statements] ;;:deleted; causes conflicts; replaced by the following:
  : procedure_call_statement sequence_of_statements_opt ;;:new
  | delay_statement sequence_of_statements_opt ;;:new
  ;

;; triggering_statement ::= procedure_call_statement | delay_statement ;; deleted, inlined to reduce conflicts

abortable_part : sequence_of_statements ;

abort_statement
  : 'abort' name {',' name} ';'
    %((wisi-statement-action [1 statement-start 5 statement-end]))%
  ;

;; Compute indent for trailing comments.
compilation
  : compilation_unit
    %((wisi-indent-action [[0 0]]))%
    %((wisi-terminate-partial-parse))%
  | compilation compilation_unit
    %((wisi-indent-action [0 [0 0]]))%
  ;

;; extended for partial parse, fragment parse
compilation_unit
  ;; context_clause library_item ;;:redundant; all are in declarative_item
  ;; context_clause subunit ;;:redundant
  : with_clause ;;:new
  | ['private'] declarative_item ;;:new
    %((wisi-indent-action [0 0]))%
  | statement ;;:new
  | subunit ;;:new
  | entry_declaration ;;:new for format parameter list
  ;

;; library_item ::=
;;     ['private'] library_unit_declaration
;;   | library_unit_body
;;   | ['private'] library_unit_renaming_declaration

;; library_unit_declaration ::=
;;     subprogram_declaration
;;   | package_declaration
;;   | generic_declaration
;;   | generic_instantiation

;; library_unit_renaming_declaration ::=
;;     package_renaming_declaration
;;   | generic_renaming_declaration
;;   | subprogram_renaming_declaration

;; library_unit_body ::= subprogram_body | package_body

;; parent_unit_name ::= name ;; inlined

;; with_clause merged into compilation_unit; use_clause is already there via declarative_item
;; context_clause ::= context_item+
;; context_item ::= with_clause | use_clause

with_clause : limited_with_clause | nonlimited_with_clause ;

limited_with_clause
  : 'limited' ['private'] 'with' name_list ';'
    %((wisi-statement-action [1 statement-start 5 statement-end])
      (wisi-face-apply-list-action [4 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-with ada-indent-broken]))%
  ;

nonlimited_with_clause
  : ['private'] 'with' name_list ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 4 statement-end])
      (wisi-face-apply-list-action [3 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil nil ada-indent-with ada-indent-broken]))%
  ;

body_stub : subprogram_body_stub | package_body_stub | task_body_stub | protected_body_stub ;

subprogram_body_stub
  : [overriding_indicator] subprogram_specification 'is' 'separate' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 6 statement-end])
      (wisi-indent-action [nil nil nil ada-indent-broken nil ada-indent-broken]))%
  ;

package_body_stub
  : 'package' 'body' IDENTIFIER 'is' 'separate' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken
                               nil ada-indent-broken nil ada-indent-broken]))%
  ;

task_body_stub
  : 'task' 'body' IDENTIFIER 'is' 'separate' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 nil font-lock-type-face])
      (wisi-indent-action [nil nil ada-indent-broken nil ada-indent-broken nil ada-indent-broken]))%
  ;

protected_body_stub
  : 'protected' 'body' IDENTIFIER 'is' 'separate' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               nil ada-indent-broken]))%
  ;

subunit
  : 'separate' '(' name ')' proper_body
    %((wisi-statement-action [1 statement-start])
      (wisi-indent-action [nil ada-indent-broken (wisi-anchored 2 1) (wisi-anchored 2 0) nil]))%
  ;

exception_declaration
  : defining_identifier_list ':' 'exception' [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 5 statement-end]))%
  ;

exception_handler_list ;;:new for pragma, action
  : exception_handler
  | pragma_g ;;:new for pragma
  | exception_handler_list exception_handler
    %((wisi-statement-action [1 motion 2 motion])
      (wisi-motion-action [1 2]))%
  ;

handled_sequence_of_statements
  : sequence_of_statements ['exception' exception_handler_list]
    %((wisi-indent-action [[nil nil]
                           (- ada-indent)
                           [(- ada-indent-when ada-indent) (- ada-indent-when ada-indent)]]))%
  ;

exception_choice_list ;;:new for action
  : exception_choice {'|' exception_choice} ;

exception_handler
  : 'when' [choice_parameter_specification ':'] exception_choice_list '=>' sequence_of_statements
    %((wisi-statement-action [1 motion])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ;; test/ada_mode-recover_30.adb Initialize exception handler
                               [(wisi-block ada-indent) ada-indent]]))%
  ;

choice_parameter_specification ::= IDENTIFIER ;

exception_choice ::= name | 'others' ;

raise_statement
  : 'raise' ';'
    %((wisi-statement-action [1 statement-start 2 statement-end]))%
  | 'raise' name ['with' expression] ';'
    %((wisi-statement-action [1 statement-start 5 statement-end])
      ;; test/ada_mode-nominal.adb
      ;; raise Constraint_Error with
      ;;   "help " &
      ;;   "me!";
      ;;
      ;; raise Constraint_Error
      ;;   with Integer'Image (1) &
      ;;   "help!";
      ;;
      ;; test/ada_mode-recover_30.adb Initialize exception handler
      (wisi-indent-action [nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken
                           (wisi-anchored% 3 ada-indent-broken) ;; same as 7.0.1
                           ada-indent-broken]))%
  ;

raise_expression
  : 'raise' name ['with' simple_expression]
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken]))%
  ;

generic_declaration : generic_subprogram_declaration | generic_package_declaration ;

generic_subprogram_declaration
  : generic_formal_part subprogram_specification [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 2 statement-override 4 statement-end])
      (wisi-motion-action [1 2 4]))%
  ;

generic_package_declaration
  : generic_formal_part package_specification ';'
    %((wisi-statement-action [1 statement-start 3 statement-end])
      (wisi-motion-action [1 2 3])
      (wisi-indent-action [[nil ada-indent] nil ada-indent-broken]))%
  ;

generic_formal_part
  : 'generic' {generic_formal_parameter_declaration}
    %((wisi-statement-action [1 statement-start])
      (wisi-indent-action [nil (wisi-block ada-indent)]))%
  ;

generic_formal_parameter_declaration
  : formal_object_declaration
  | formal_type_declaration
  | formal_subprogram_declaration
  | formal_package_declaration
  | use_clause ;;:new, moved here from generic_formal_part
  | pragma_g ;;:new for pragma
  ;

generic_instantiation
  : 'package' name 'is' 'new' name [aspect_specification] ';' ;;:edited; generic_actual_part redundant with actual_parameter_part in function_call in name
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-name-action 2)
      (wisi-face-apply-action [2 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil ada-indent-broken]))%
  | [overriding_indicator] 'procedure' name 'is' 'new' name [aspect_specification] ';' ;;:edited; generic_actual_part covered by name
    %((wisi-statement-action [1 statement-start 2 statement-override 8 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil ada-indent-broken]))%
  | [overriding_indicator] 'function' name 'is' 'new' name [aspect_specification] ';' ;;:edited; generic_actual_part covered by name
    %((wisi-statement-action [1 statement-start 2 statement-override 8 statement-end])
      (wisi-name-action 3)
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil
                           nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           nil ada-indent-broken]))%
  ;

;; generic_actual_part ;;:redundant with actual_parameter_part
;;  : '(' generic_association_list ')'

;; generic_association ;;: redundant with parameter_association
;;  : selector_name assoc_expression
;;  | expression

;; explicit_generic_actual_parameter ;;:inlined
;;   : expression
;;   ;; name ;;:redundant <variable_>name
;;   ;; name ;;:redundant <subprogram_>name
;;   ;; name ;;:redundant <entry_>name
;;   ;; name ;;:redundant subtype_mark
;;   ;; name ;;:redundant <package_instance_>name
;;   ;

formal_object_declaration
  : defining_identifier_list ':' mode [null_exclusion] name [assign_value] [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 8 statement-end])
      (wisi-face-apply-action [5 font-lock-function-name-face font-lock-type-face])
      (wisi-indent-action [nil
                           ada-indent-broken ada-indent-broken ada-indent-broken
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           (wisi-hanging% (wisi-anchored% 5 ada-indent-broken)
                                          (wisi-anchored% 5 (* 2 ada-indent-broken)))
                           ada-indent-broken ada-indent-broken]))%
  | defining_identifier_list ':' mode access_definition [assign_value] [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging% (wisi-anchored% 4 ada-indent-broken)
                                              (wisi-anchored% 4 (* 2 ada-indent-broken)))
                               ada-indent-broken ada-indent-broken]))%
  ;

formal_type_declaration : formal_complete_type_declaration | formal_incomplete_type_declaration ;

formal_complete_type_declaration
  : 'type' IDENTIFIER [discriminant_part] 'is' formal_type_definition [ 'or' 'use' name ] [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 10 statement-end])
      (wisi-face-apply-action [2 nil font-lock-type-face
                                 8 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken]))%
  ;

formal_incomplete_type_declaration
  : 'type' IDENTIFIER [discriminant_part] ['is' 'tagged'] ['or' 'use' name] ';'
    %((wisi-statement-action [1 statement-start 9 statement-end])
      (wisi-face-apply-action [2 nil font-lock-type-face
                                 8 nil font-lock-type-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken]))%
  ;

formal_type_definition ::=
    formal_private_type_definition
  | formal_derived_type_definition
  | formal_discrete_type_definition
  | formal_signed_integer_type_definition
  | formal_modular_type_definition
  | formal_floating_point_definition
  | formal_ordinary_fixed_point_definition
  | formal_decimal_fixed_point_definition
  | formal_array_type_definition
  | formal_access_type_definition
  | formal_interface_type_definition
  ;

formal_private_type_definition ::= [['abstract'] 'tagged'] ['limited'] 'private' ;

formal_derived_type_definition
  : ['abstract'] ['limited' | 'synchronized'] 'new' name [['and' interface_list] 'with' 'private']
    %((wisi-face-apply-action [5 font-lock-function-name-face font-lock-type-face]))%
  ;

formal_discrete_type_definition ::= '(' '<>' ')' ;

formal_signed_integer_type_definition ::= 'range' '<>' ;

formal_modular_type_definition ::= 'mod' '<>' ;

formal_floating_point_definition ::= 'digits' '<>' ;

formal_ordinary_fixed_point_definition ::= 'delta' '<>' ;

formal_decimal_fixed_point_definition ::= 'delta' '<>' 'digits' '<>' ;

formal_array_type_definition ::= array_type_definition ;

formal_access_type_definition ::= access_type_definition ;

formal_interface_type_definition ::= interface_type_definition ;

formal_subprogram_declaration : formal_concrete_subprogram_declaration | formal_abstract_subprogram_declaration ;

formal_concrete_subprogram_declaration
  : 'with' subprogram_specification ['is' subprogram_default] [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 6 statement-end])
      (wisi-indent-action [nil nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken]))%
  ;

formal_abstract_subprogram_declaration
  : 'with' subprogram_specification 'is' 'abstract' [subprogram_default] [aspect_specification] ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-indent-action [nil nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken]))%
  ;

subprogram_default
  : default_name
  | '<>'
  | 'null'
  ;

default_name
  : name
    %((wisi-face-apply-action [1 font-lock-function-name-face font-lock-function-name-face]))%
  ;

formal_package_declaration
  : 'with' 'package' IDENTIFIER 'is' 'new' name [aspect_specification] ';'
    ;;:edited formal_package_actual_part redundant with actual_parameter_part in name
    %((wisi-statement-action [1 statement-start 8 statement-end])
      (wisi-face-apply-action [3 font-lock-function-name-face font-lock-function-name-face
                                 6 font-lock-function-name-face font-lock-function-name-face])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               (wisi-hanging* ada-indent-broken ada-indent-broken)
                               ada-indent-broken ada-indent-broken]))%
  ;

;; formal_package_actual_part ;;:redundant with expanded formal_part
;;  : '(' ['others' '=>'] '<>' ')'
;; | [generic_actual_part] ;;:redundant with formal_part
;; | '(' formal_package_association {',' formal_package_association} [',' 'others' '=>' '<>'] ')'

;; formal_package_association ;;:redundant; covered by expanded formal_part
;;   : generic_assocation_list | selector_name '=>' '<>'

aspect_clause
  : attribute_definition_clause
  | enumeration_representation_clause
  | record_representation_clause
  | at_clause
  ;

;; local_name ;;:redundant with attribute_designator in name; inlined to "name"
;;   : direct_name tick attribute_designator
;;   | name

aspect_association ;;:new for wisi-ada.adb code.
  : aspect_mark ['=>' aspect_definition]
    ;; test/aspects.ads, test/ada_mode-nominal.adb task Executive;
    %((wisi-indent-action [nil nil (ada-indent-aspect)]))%
  ;

aspect_mark_list
  : aspect_association
  | aspect_mark_list ',' aspect_association
  ;

aspect_specification
  : 'with' aspect_mark_list
    ;; test/ada_mode-recover_30.adb "--  comment after aspect_specification"
    ;; comment is indented by higher level.
    %((wisi-indent-action [nil ada-indent-broken]))%
  ;

aspect_mark : IDENTIFIER [tick IDENTIFIER] ; ;;:edited "class" changed
;; to IDENTIFIER so foo'class is legal elsewhere

aspect_definition
  ;; name ;; redundant with expression
  : expression
  ;; indent done in aspect_association
  ;; IDENTIFIER ;;:redundant with expression
  ;; aggregate ;;:redundant with expression
  | global_aspect_definition
  ;

attribute_definition_clause
  : 'for' name tick attribute_designator 'use' expression ';'
    %((wisi-statement-action [1 statement-start 7 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken nil]))%
  ;; 'for' name tick attribute_designator 'use' name ';' ;;:redundant with expression
  ;

enumeration_representation_clause
  : 'for' name 'use' enumeration_aggregate ';'
    %((wisi-statement-action [1 statement-start 5 statement-end])
      (wisi-face-apply-action [2 font-lock-function-name-face font-lock-type-face])
      (wisi-indent-action [nil
                           (wisi-hanging* ada-indent-broken ada-indent-broken)
                           ada-indent-broken ada-indent-broken ada-indent-broken]))%
  ;

enumeration_aggregate : array_aggregate ;

record_representation_clause
  : 'for' name 'use' 'record' [mod_clause] {component_clause} 'end' 'record' [name] ';'
    %((wisi-statement-action [1 statement-start 10 statement-end])
    (wisi-face-apply-action [2 font-lock-function-name-face font-lock-type-face
                               9 font-lock-function-name-face font-lock-type-face])
    (wisi-indent-action [nil
                         (wisi-hanging* ada-indent-broken ada-indent-broken)
                         ada-indent-broken
                         [(ada-indent-record* 'FOR 4 0) (ada-indent-record* 'FOR 4 ada-indent)]
                         (ada-indent-record 1 4 ada-indent)
                         [(ada-indent-record* 'FOR 4 ada-indent) (ada-indent-record* 'FOR 4 ada-indent)]
                         (ada-indent-record* 'FOR 4 0)
                         ada-indent-broken
                         (wisi-hanging* ada-indent-broken ada-indent-broken)
                         ada-indent-broken]))%
  ;

component_clause
  : name 'at' position 'range' first_bit '..' last_bit ';'
    %((wisi-statement-action [1 statement-start 8 statement-end])
      (wisi-indent-action [nil ada-indent-broken ada-indent-broken ada-indent-broken ada-indent-broken
                               ada-indent-broken ada-indent-broken ada-indent-broken]))%
  ;

position : expression ;

first_bit : simple_expression ;

last_bit : simple_expression ;

;; code_statement ;;:deleted; redundant with procedure_call_statement
;; ::= qualified_expression ';'

;; storage_pool_indicator ;;:deleted; not used in the grammar
;; ::= name | 'null' | Standard

;; restriction ;;:deleted; not used in the grammar
;; ::= IDENTIFIER | IDENTIFIER '=>' restriction_parameter_argument

;; restriction_parameter_argument ;;:deleted; not used in the grammar
;; ::= name | expression

extended_global_aspect_definition ::= 'use' formal_parameter_designator | 'do' dispatching_operation_specifier ;

extended_global_aspect_element ::=
    'use' formal_parameter_set
  | 'do' dispatching_operation_set
  ;

extended_global_mode ::= 'overriding' basic_global_mode ;

formal_parameter_designator ::= formal_group_designator | name ;

formal_parameter_set ::= formal_group_designator | name {',' name} ;

formal_group_designator ::= 'null' | 'all' ;

;; formal_parameter_name ::= ;; inlined to "name"
;;     subtype_mark
;;   | name
;;   | name

dispatching_operation_set ::= dispatching_operation_specifier {',' dispatching_operation_specifier} ;

dispatching_operation_specifier ::= name  '(' name ')' ;

delta_constraint
  : 'delta' simple_expression [range_constraint]
    ;; constistent with ada-mode 3.1
    %((wisi-indent-action [nil ada-indent-broken ada-indent-broken]))%
  ;

at_clause
  : 'for' direct_name 'use' 'at' expression ';'
    %((wisi-statement-action [1 statement-start 6 statement-end]))%
  ;

mod_clause ::= 'at' 'mod' expression ';' ;

;; end of file
